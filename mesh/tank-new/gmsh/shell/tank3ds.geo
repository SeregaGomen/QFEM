//
// ТОПЛИВНЫЙ БАК РК "ЦИКЛОН-4"
//

// ПАРАМЕТРЫ 

// Масштабный коэффициент
a = 1000;

// Диаметр
d = 3900 / a;
// Общая высота
h = 16691 / a;
// Расстояние до фальшднища
l = 6008 / a;
// Ширина фальшднища
p = 169 / a;
// Высота фальшднища
q = 169 / a;
// Радиус днища
r = 2500 / a;
// Высота днища
//hd = (18000 - 16691) / a;
hd = 935 / a;
// Толщина шпангоута
k = 100 / a;





// Параметр густоты сетки
grid = 0.25;

// ОПОРНЫЕ ТОЧКИ
// Боковая стенка
Point(1) = { -d / 2, 0, 0, grid };
Point(2) = { -d / 2, 0, -l, grid };
Point(3) = { -d / 2 + p, 0, -l, grid };
Point(4) = { -d / 2, 0, -l - q, grid };
Point(5) = { -d / 2, 0, -h, grid };
// Центры сфер
Point(6) = { 0, 0, hd - r, grid };
Point(7) = { 0, 0, r - h - hd, grid };
// Полюсы днищ
Point(8) = { 0, 0, hd, grid };
Point(9) = { 0, 0, - h - hd, grid };
// Шпангоуты
Point(10) = { -d / 2, 0, k, grid };
Point(11) = { -d / 2, 0, -h - k, grid };
Point(12) = { -Sqrt(r^2 - (k - (hd - r))^2), 0, k, grid };
Point(13) = { -Sqrt(r^2 - (- h - k - (r - h - hd))^2), 0, -h - k, grid };
// Нижняя юбка
Point(14) = { -d / 2, 0, -h - hd, grid };


// ЛИНИИ
Line(1) = { 1, 2 };
Line(2) = { 2, 3 };
Line(3) = { 3, 4 };
Line(4) = { 4, 5 };

Circle(5) = { 1, 6, 8 };
Circle(6) = { 5, 7, 9 };

Line(7) = { 1, 10 };
Line(8) = { 5, 11 };
Line(9) = { 10, 12 };
Line(10) = { 11, 13 };
Line(11) = { 11, 14 };


// ПОВЕРХНОСТИ
Extrude{ { 0, 0, 1 }, { 0, 0, 0 }, Pi/2 } { Line{ 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11 }; }



